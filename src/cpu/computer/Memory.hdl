// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    // ram:      0x0000 = b000000000000000
    //           0x3FFF = b011111111111111
    // screen:   0x4000 = b100000000000000
    //           0x5FFF = b101111111111111
    // keyboard: 0x6000 = b110000000000000

    PARTS:
    // address == 11* => keyboard is selected
    And(a=address[14], b=address[13], out=keyboardSel);

    // address == 10* => screen is selected
    Not(in=address[13], out=not13);
    And(a=address[14], b=not13, out=ramOrScreenSel);

    // Demultiplexer to select which memory to load (if any)
    DMux(in=load, sel=ramOrScreenSel, a=ramLoad, b=screenLoad);
    
    // Memory units
    RAM16K(in=in, load=ramLoad, address=address[0..13], out=ramOut);
    Screen(in=in, load=screenLoad, address=address[0..12], out=screenOut);
    Keyboard(out=keyboardOut);

    // Multiplexers to output the correct memory
    Mux16(a=ramOut, b=screenOut, sel=ramOrScreenSel, out=ramOrScreenOut);
    Mux16(a=ramOrScreenOut, b=keyboardOut, sel=keyboardSel, out=out);
}